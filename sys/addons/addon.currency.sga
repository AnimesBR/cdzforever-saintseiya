;; ----------------------------------------------------------------------------
;; Addon :: Currency/Câmbio
;; Faz a conversão de valor entre duas determinadas moedas
;; ----------------------------------------------------------------------------
;; Uso: !currency <valor> <moeda> to <moeda>
;;      !câmbio <valor> <moeda> to <moeda>
;;      !currency search <moeda>
;;      !câmbio search <moeda>
;; ----------------------------------------------------------------------------

alias com_all_!câmbio com_all_!currency $1-
alias com_all_!cambio com_all_!currency $1-
alias com_all_!currency {
  if ($1 == search) { com_aux_!currency_search $2- | return }

  if (!$regex($1-,([0-9]+([.,][0-9]{2}){0,1}) ([A-Za-z]{3}) to ([A-Za-z]{3}))) msgehalt Formato 4incorreto15.  0,14 15,1 Use 8!Currency <valor> <moeda> to <moeda> 0,14 15,1 Exemplo: 9!Currency 1 BRL to USD
  if ($regml(4)) tokenize 32 $regml(1) $regml(3) $regml(4)
  else tokenize 32 $regml(1) $regml(2) $regml(3)

  if ($currency($2).name == $null) msgehalt A moeda9 $2 4não 15está cadastrada.
  if ($currency($3).name == $null) msgehalt A moeda9 $3 4não 15está cadastrada.

  currency error 1
  currency nick $nick
  currency target $starget
  site_get_contents $+(http://cdzforever.net/saintseiya_handlers/currency.php?a=,$1,&from=,$2,&to=,$3) currency.callback.read currency.callback.end
}
alias com_aux_!currency_search {
  if (!$1) msgehalt Você precisa indicar um 0nome de moeda 15para procurar. 0,14 15,1 Ex: 8!Currency Search Real
  if ($currency($1-).sigla == $null) msgehalt Nenhuma sigla para0 $1- 15foi encontrada.
  var %sigla = $v1
  noticen A sigla para11 $currency(%sigla).name 15é8 %sigla
}

;; -------------------------------------------------------------------- Aliases

alias -l currency {
  if ($isid) {
    if ($prop == name) return $read($scriptdir $+ list.currency,s,$1)
    if ($prop == sigla) return $gettok($read($scriptdir $+ list.currency,w,* $+ $1- $+ *),1,32)
    return $hget(addon.currency,$prop)
  }
  hadd -m addon.currency $1-
}
alias -l currency.clean hdel -w addon.currency *

;; -------------------------------------------------------------------- Callbacks

alias currency.callback.read {
  if ($numtok($1-,59) > 1) {
    currency error 0
    msgto $currency().target 00 $+ $currency().nick $+ 15,8 $gettok($1,1,59) $currency($gettok($1,2,59)).name 14=9 $gettok($1,3,59) $currency($gettok($1,4,59)).name
    sockclose $sockname
  }
}

alias currency.callback.end {
  if ($currency().error) noticeto $currency().nick Não foi possível fazer esta conversão. Verifique os valores novamente.
  currency.clean
}
